#!/usr/bin/perl -w

use strict;
use warnings;
use Getopt::Long qw(:config no_ignore_case);
use Text::ASCIITable;
use Perun::Agent;
use Perun::Common qw(printMessage printTable getSortingFunction);

sub help {
	return qq{
	Lists all required user_facility attributes. User id and facility id or name are required options. Resource id is optional.
	-----------------------------------------------------------------------
	Available options:
	--facilityId   | -f facility id
	--facilityName | -F facility name
	--userId       | -u user id
	--resourceId   | -r resource to get services from
	--orderById    | -i order by attribute id
	--orderByName  | -n order by attribute friendly name
	--batch        | -b batch
	--help         | -h help

	};
}

our $batch;
my ($facilityId, $facilityName, $userId, $resourceId, $sortingFunction);
GetOptions ("help|h"   => sub {
		print help();
		exit 0;
	}, "batch|b"       => \$batch,
	"facilityId|f=i"   => \$facilityId,
	"facilityName|F=s" => \$facilityName,
	"userId|u=i"       => \$userId,
	"resourceId|r=i"   => \$resourceId,
	"orderById|i"      => sub { $sortingFunction = getSortingFunction('getId') },
	"orderByName|n"    => sub {$sortingFunction = getSortingFunction("getFriendlyName", 1); } ) || die help();

unless (defined $sortingFunction) { $sortingFunction = getSortingFunction("getFriendlyName", 1); }

# Check options
unless (defined($facilityId) or (defined($facilityName))) { die "ERROR: facilityId  or facilityName is required \n";}
unless (defined($userId)) { die "ERROR: userId is required \n";}

my $agent = Perun::Agent->new();
my $facilitiesAgent = $agent->getFacilitiesAgent;

unless ($facilityId) {
	my $facility = $facilitiesAgent->getFacilityByName( name => $facilityName );
	$facilityId = $facility->getId;
}

my $attributesAgent = $agent->getAttributesAgent;
my @attributes;
if ($resourceId) {
	@attributes = $attributesAgent->getRequiredAttributes( facility => $facilityId, user => $userId,
		resourceToGetServicesFrom                                   => $resourceId );
} else {
	@attributes = $attributesAgent->getRequiredAttributes( facility => $facilityId, user => $userId );
}

unless (@attributes) {
	printMessage "No required attributes found", $batch;
	exit 0;
}

printTable($sortingFunction, @attributes);
